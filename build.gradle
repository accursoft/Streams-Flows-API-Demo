buildscript {
    ext.kotlin_version = '1.3.10'
    ext.ktor_version = '1.0.0'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'io.ktor.server.netty.EngineMain'

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlinx/' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile "ch.qos.logback:logback-classic:+"
    compile "io.ktor:ktor-client-cio:$ktor_version"
    compile "io.ktor:ktor-client-json:$ktor_version"
    compile "io.ktor:ktor-client-gson:$ktor_version"
    compile "io.ktor:ktor-locations:$ktor_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task buildDocker(type: Exec, dependsOn: installDist) {
    group 'docker'
    commandLine 'docker', 'build', '-t=accursoft/streams-designer-api:latest', '-f=Dockerfile', 'build/install/api-example'
}

task testDocker(type: Exec, dependsOn: buildDocker) {
    group 'docker'
    commandLine './testDocker.sh'
}

task publishDocker(type: Exec, dependsOn: testDocker) {
    group 'docker'
    commandLine 'docker', 'push', 'accursoft/streams-designer-api'
}